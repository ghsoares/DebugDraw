[gd_resource type="Shader" format=2]

[resource]
code = "shader_type spatial;
render_mode unshaded, depth_draw_alpha_prepass;

varying vec2 size;

void vertex() {
	vec3 wpa = VERTEX;
	vec3 wpb = VERTEX + NORMAL;
	
	vec2 scl = UV / VIEWPORT_SIZE;
	scl.y *= VIEWPORT_SIZE.y / VIEWPORT_SIZE.x;
	
	vec4 vpa = (INV_CAMERA_MATRIX * vec4(wpa, 1.0));
	vec4 vpb = (INV_CAMERA_MATRIX * vec4(wpb, 1.0));
	
	if (PROJECTION_MATRIX[3][3] != 0.0) {
		float h = abs(1.0 / (2.0 * PROJECTION_MATRIX[1][1]));
		float sc = (h * 2.0); //consistent with Y-fov
		scl *= sc;
	} else {
		scl *= min(-vpa.z, -vpb.z);
	}
	
	vec3 off = vpb.xyz - vpa.xyz;
	vec3 sy = off.x == 0.0 && off.y == 0.0 ? vec3(0.0, 1.0, 0.0) : normalize(vec3(-off.y, off.x, 0.0));
	vec3 sx = vec3(sy.y, -sy.x, sy.z);
	vec3 s = (sx * scl.x + sy * scl.y);
	
	vec4 p = vpa;
	p.xyz += off * max(sign(UV.x), 0.0);
	p.xyz += s;
	p = PROJECTION_MATRIX * p;
	
	POSITION = p;
	
	float r = abs(scl.y);
	
	size = vec2(length(off.xy) * 0.5 + r, r);
	UV = UV / (abs(UV));
}

void fragment() {
	vec2 p = UV * size;
	float r = min(size.x, size.y);
	vec2 b = size - r;
	vec2 d = abs(p) - b;
	float dst1 = min(max(d.x, d.y), 0.0);
	float dst2 = length(max(d, 0.0));
	float dst = (dst1 + dst2) - r;
	
	ALPHA = -dst;
	ALBEDO *= COLOR.rgb;
	//ALPHA *= COLOR.a;
	ALPHA_SCISSOR = 0.0;
}




"
